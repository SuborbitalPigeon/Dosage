pkgdatadir = join_paths(get_option('datadir'), meson.project_name())
datadir = get_option('prefix') / get_option('datadir')

blueprints = custom_target('blueprints',
	input: files(
		'ui/window.blp',
		'ui/med-window.blp',
		'ui/preferences.blp',
		'gtk/help-overlay.blp',
	),
	output: '.',
	command: [find_program('blueprint-compiler'), 'batch-compile', '@OUTPUT@', '@CURRENT_SOURCE_DIR@', '@INPUT@'],
)

data_res = gnome.compile_resources('com.github.diegopvlk.Dosefy.data',
	'com.github.diegopvlk.Dosefy.data.gresource.xml',
	gresource_bundle: true,
	install: true,
	install_dir: pkgdatadir,
	dependencies: blueprints,
)

desktop_file = i18n.merge_file(
	input: 'com.github.diegopvlk.Dosefy.desktop.in',
	output: 'com.github.diegopvlk.Dosefy.desktop',
	type: 'desktop',
	po_dir: '../po',
	install: true,
	install_dir: join_paths(get_option('datadir'), 'applications')
)

desktop_utils = find_program('desktop-file-validate', required: false)
if desktop_utils.found()
	test('Validate desktop file', desktop_utils, args: [desktop_file])
endif

appstream_file = i18n.merge_file(
	input: 'com.github.diegopvlk.Dosefy.appdata.xml.in',
	output: 'com.github.diegopvlk.Dosefy.appdata.xml',
	po_dir: '../po',
	install: true,
	install_dir: join_paths(get_option('datadir'), 'appdata')
)

appstream_util = find_program('appstream-util', required: false)
if appstream_util.found()
	test('Validate appstream file', appstream_util, args: ['validate', appstream_file])
endif

install_data('com.github.diegopvlk.Dosefy.gschema.xml',
	install_dir: join_paths(get_option('datadir'), 'glib-2.0/schemas')
)

compile_schemas = find_program('glib-compile-schemas', required: false)
if compile_schemas.found()
	test('Validate schema file',
		compile_schemas,
		args: ['--strict', '--dry-run', meson.current_source_dir()])
endif

subdir('icons')